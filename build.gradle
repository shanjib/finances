buildscript {
    repositories {
        mavenCentral()
        maven { url "https://maven.vaadin.com/vaadin-addons" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:2.0.9.RELEASE")
    }
}

apply plugin: 'application'
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

bootJar {
    baseName = 'gs-spring-boot'
    version = '0.1.0'
}

repositories {
    mavenCentral()
    maven { url "https://maven.vaadin.com/vaadin-addons" }
}

dependencyManagement {
	imports {
		mavenBom 'com.vaadin:vaadin-bom:14.0.9'
	}
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

configurations {
    /* Exclude to use log4j instead */
    compile.exclude module: 'spring-boot-starter-logging'

    /* Exclude to use jetty instead */
    compile.exclude module: 'spring-boot-starter-tomcat'
    compile.exclude module: 'tomcat-jdbc'
}

dependencies {
    /* Lombok because it's great */
    compile("org.projectlombok:lombok")
    compile("com.google.guava:guava:18.0")

    /* Spring Boot Starter */
    compile("org.springframework.boot:spring-boot-starter")
    compile("org.springframework.boot:spring-boot-starter-web")
    compile("org.springframework.boot:spring-boot-starter-jetty")
    compile("org.springframework.boot:spring-boot-starter-thymeleaf")

    /* Database Dependencies */
    compile("org.springframework.boot:spring-boot-starter-data-jpa")
    compile("mysql:mysql-connector-java")
    compile("com.h2database:h2")
    compile("javax.xml.bind:jaxb-api")
    compile("org.javassist:javassist:3.23.1-GA")

    /* Log4j instead of logback */
    compile("org.springframework.boot:spring-boot-starter-log4j2")
}

project.ext {
    baseJvmArgs = [
            "-Dspring.profiles.active=local"
    ]

    localDbArgs = [
            "-Dspring.jpa-show-sql=true",
            "-Dspring.datasource.url=jdbc:h2:~/localdb",
            "-Dspring.h2.console.path=/h2-console",
            "-Dspring.h2.console.enabled=true"
    ]
    persistDbArgs = [
            "-Dspring.jpa-show-sql=true",
            "-Dspring.datasource.url=jdbc:h2:file:./data/sample",
            "-Dspring.h2.console.path=/h2-console",
            "-Dspring.h2.console.enabled=true"
    ]
}

applicationDefaultJvmArgs = project.baseJvmArgs + project.persistDbArgs